#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/ij8Lv.png
#

# Load dependencies.
pmodload 'helper'

prompt_jinks_help () {
  cat <<EOH
This prompt is color-scheme-able.  You can invoke it thus:

  prompt jinks [<hostname+clock-color> [<user-color> [<root-color>]]]

The default colors are green for host and green/red for user/root.
This theme works best with a dark background.

EOH
}

function prompt_jinks_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_jinks_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_jinks_precmd

  zstyle ':prezto:module:editor' completing '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:keymap:primary' overwrite ' %F{red}♺%f'
  zstyle ':prezto:module:editor:keymap' primary ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:keymap' alternate ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'
  zstyle ':prezto:module:git' action ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git' added ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git' ahead ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git' behind ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git' branch ':%F{green}%b%f'
  zstyle ':prezto:module:git' commit ':%F{green}%.7c%f'
  zstyle ':prezto:module:git' deleted ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git' modified ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git' position ':%F{green}%p%f'
  zstyle ':prezto:module:git' renamed ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git' stashed ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git' unmerged ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git' untracked ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git' info \
    'prompt'  ' %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Variables
  local host_col=${1:-'green'}
  local user_col=${2:-'green'}
  local root_col=${3:-'red'}
  local parens_col=blue

  local lpar="%B%F{$parens_col}(%f%b"
  local rpar="%B%F{$parens_col})%f%b"
  local lbra="%B%F{$parens_col}%\[%f%b"
  local rbra="%B%F{$parens_col}%\]%f%b"

  local clock="$lpar%B%F{$host_col}%T%f%b$rpar"
  local userstr="%(!.%B%F{$root_col}.%B%F{$user_col})%n%f%b"
  local hoststr="%F{yellow}@%f%B%F{$host_col}%m%f%b"
  local dirstr="%B%F{cyan}%~%f%b"

  # Define prompts.
  PROMPT="${clock}${lbra}${userstr}${hoststr}${rbra}${dirstr}\${git_info:+\${(e)git_info[prompt]}} %(!. %B%F{red}#%f%b.%%)\${editor_info[keymap]} "
  PS2="$lpar%B%F{cyan}%_%f%b$rpar%B%F{yellow} ->%f%b "
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]}'
  SPROMPT="zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? "
}

prompt_jinks_preview () {
  local color colors
  colors=(red yellow green blue magenta)

  if (( ! $#* )); then
    for (( i = 1; i <= $#colors; i++ )); do
      color=$colors[$i]
      prompt_preview_theme jinks $color
      (( i < $#colors )) && print
    done
  else
    prompt_preview_theme jinks "$@"
  fi
}

prompt_jinks_setup "$@"

# vim: ft=zsh:
